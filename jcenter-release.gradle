apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

group = BINTRAY_GROUP_ID
version = VERSION_NAME

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name BINTRAY_DESCRIPTION
                description BINTRAY_DESCRIPTION
                url BINTRAY_WEBSITE_URL
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
                developers {
                    developer {
                        id BINTRAY_DEVELOPER_ID
                        name BINTRAY_DEVELOPER_NAME
                        email BINTRAY_DEVELOPER_EMAIL
                    }
                }
                scm {
                    connection "${BINTRAY_WEBSITE_URL}".replace("https://", "scm:git:git://")
                    developerConnection "${BINTRAY_WEBSITE_URL}".replace("https://", "scm:git:git://")
                    url BINTRAY_WEBSITE_URL
                }
                issueManagement {
                    system 'GitHub'
                    url "${BINTRAY_WEBSITE_URL}/issues"
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts {
    archives javadocJar
}

android.libraryVariants.all { variant ->
    println variant.javaCompile.classpath.files
    if(variant.name == 'release') { //我们只需 release 的 javadoc
        task("generate${variant.name.capitalize()}Javadoc", type: Javadoc) {
            // title = ''
            // description = ''
            source = variant.javaCompile.source
            classpath = files(variant.javaCompile.classpath.files, project.android.getBootClasspath())
            options {
                encoding "utf-8"
                links "http://docs.oracle.com/javase/7/docs/api/"
                linksOffline "http://d.android.com/reference", "${android.sdkDirectory}/docs/reference"
            }
            exclude '**/BuildConfig.java'
            exclude '**/R.java'
        }
        task("javadoc${variant.name.capitalize()}Jar", type: Jar, dependsOn: "generate${variant.name.capitalize()}Javadoc") {
            classifier = 'javadoc'
            from tasks.getByName("generate${variant.name.capitalize()}Javadoc").destinationDir
        }
        artifacts {
            archives tasks.getByName("javadoc${variant.name.capitalize()}Jar")
        }
    }
}

def getBintrayUserProperty() {
    return hasProperty('BINTRAY_USER') ? BINTRAY_USER : ""
}

def getBintrayApiKeyProperty() {
    return hasProperty('BINTRAY_APIKEY') ? BINTRAY_APIKEY : ""
}

bintray {
    user = getBintrayUserProperty()
    key = getBintrayApiKeyProperty()

    configurations = ['archives']

    pkg {
        repo = 'maven'
        name = project.name
        desc = BINTRAY_DESCRIPTION
        websiteUrl = BINTRAY_WEBSITE_URL
        vcsUrl = "${BINTRAY_WEBSITE_URL}.git"
        issueTrackerUrl = "${BINTRAY_WEBSITE_URL}/issues"
        licenses = ['Apache-2.0']
        dryRun = false
        publish = true
        version {
            name = VERSION_NAME
            vcsTag = VERSION_NAME
        }
    }
}
